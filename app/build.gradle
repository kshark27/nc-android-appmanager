import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def VERSION_CODE = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
def VERSION_NAME = "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
def APP_NAME = "AppManager"
def APP_NAME_DEBUG = "AppManager-debug"

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("ddMMyyyyHHmm")
    return df.format(Calendar.getInstance().getTime())
}

android {
    compileSdkVersion 23
    buildToolsVersion "24.0.2"
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(output.outputFile.name, APP_NAME + "-" + VERSION_NAME + "-" + buildTime() + "-" + gitSha() + ".apk"))
        }
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "com.nhancv.appmanager"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appName: APP_NAME]
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appName: APP_NAME_DEBUG]
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        dexInProcess true
        preDexLibraries true
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation';
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'


    compile 'at.amartinz:execution:1.0.0'
    compile 'at.amartinz:hardware:1.0.1'
    // see https://github.com/Evisceration/MaterialPreferences
    compile 'com.github.Evisceration:MaterialPreferences:master-SNAPSHOT'

    // see https://github.com/pilgr/Paper
    compile 'io.paperdb:paperdb:1.5'
    // see https://github.com/EsotericSoftware/kryo
    compile 'com.esotericsoftware:kryo:3.0.3'
    // see https://github.com/magro/kryo-serializers
    compile 'de.javakaffee:kryo-serializers:0.38'

    // see https://github.com/JakeWharton/timber
    compile 'com.jakewharton.timber:timber:4.3.0'

    // see https://github.com/bumptech/glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // see https://github.com/koush/ion
    compile 'com.koushikdutta.ion:ion:2.1.8'

    def supportLibVersion = '24.2.0'
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:support-v13:${supportLibVersion}"

    // see https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'

    // see https://github.com/PhilJay/MPAndroidChart
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'

    // see https://github.com/Kennyc1012/BottomSheet
    compile 'com.github.Kennyc1012:BottomSheet:2.3.1'

    // see https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'
    // see https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.1.9'

    // see https://github.com/google/gson
    compile 'com.google.code.gson:gson:2.7'
    // see https://github.com/square/okhttp
    compile 'com.squareup.okhttp3:okhttp:3.4.1'

}
